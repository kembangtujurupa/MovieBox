apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

android {
    if (project.hasProperty('devBuild')) {
        splits.abi.enable = false
        splits.density.ebable = false
        aaptOptions.cruncherEnabled = false
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.majorik.moviebox"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
        }

        buildTypes.each {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
            def keyTmdb = properties.getProperty("keyTmdb", "")

            it.buildConfigField 'String', "TMDB_API_KEY", keyTmdb

            it.resValue 'string', "api_key", keyTmdb
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

dependencies {
    def diDependencies = rootProject.ext.diDependencies
    def networkDependencies = rootProject.ext.networkDependencies
    def logDependencies = rootProject.ext.logDependencies

    implementation project(':data')
    implementation project(':domain')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31'
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation logDependencies.timber
    implementation diDependencies.koinScope
    implementation diDependencies.koinViewModel
    implementation diDependencies.koinAndroidExt
    implementation networkDependencies.retrofit2
    implementation networkDependencies.retrofit2Moshi
    implementation networkDependencies.okHttp3
    implementation networkDependencies.retrofitCoroutinesAdapter
}
